buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.0.0'
	}
}
allprojects {
	repositories {
		jcenter()
		mavenCentral()
	}
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "eu.siacs.conversations.voicerecorder"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 2
        versionName "0.1"
    }

    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                storePassword mStorePassword
                keyAlias mKeyAlias
                keyPassword mKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    applicationVariants.all { variant ->
                if (variant.name.equals('release')) {
                        variant.outputs.each { output ->
                                if (output.zipAlign != null) {
                                        output.zipAlign.outputFile = new File(output.outputFile.parent, rootProject.name + "-${variant.versionName}.apk")
                                }
                                output.packageApplication.outputFile = new File(output.outputFile.parent, output.packageApplication.outputFile.name
                                        .replace(".apk", "-${variant.versionName}.apk"))
                        }
                }
    }
        

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
